# Maintainer: Tim Stahlhut <stahta01@gmail.com>

_realname=kicad
#_git_commit=4db10d419dd3bfd09f055fc52cd965fb7204e97e # 5.99.0.r9716  Does not have GCC 12.1 silent error bug
#_git_commit=18037e2f65dbdca73dbe3e5d375fa5f86bd33f20 # 5.99.0.r9717  Has GCC 12.1 silent error bug
#_git_commit=222c4ce7df2433650b62a8cd7ac168b2b233f685 # 5.99.0.r12634 Has GCC 12.1 silent error bug
#_git_commit=cd8e464339bca217247e51fafc1d6aa3c79260cc # 5.99.0.r12833 fails to set default ucrt64 config
#_git_commit=e867a4fd27fba140ea90266d373ba20b4768331a # 5.99.0.r12832 works to set default ucrt64 config
_wx_basever=3.1
_sourcedir=${_realname}-git
pkgbase=mingw-w64-${_realname}-git
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=6.99.0.r2045.g7ede2c70bd
pkgrel=1
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.kicad.org/"
license=("GPLv3+")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
options=('!strip')
#if [[ "${MSYSTEM}" == "UCRT64" ]]; then
  #options+=('debug')
#else
  options+=('!debug')
#fi
depends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-ngspice"
  "${MINGW_PACKAGE_PREFIX}-python"
#  "${MINGW_PACKAGE_PREFIX}-wxPython"
  "${MINGW_PACKAGE_PREFIX}-wxmsw${_wx_basever}"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-freeglut"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]] && echo "${MINGW_PACKAGE_PREFIX}-opencascade")
  "${MINGW_PACKAGE_PREFIX}-cairo"
)
makedepends=(
  'git' 'make' 'patch'
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-pkg-config"
  "${MINGW_PACKAGE_PREFIX}-swig"
)
source=(
  '002-ki-6.99-cmake-fixes-for-MINGW-CLANG.patch'
  '003-ki-6.0-code-fixes-for-GNUC-CLANG.patch'
  '005-ki-6.0-rewrite-kiwin32_rc_for_clang.patch'
  '006-ki-6.0-manifest-remove-win10-11-manifest-support.patch'
  '007-ki-6.0-Remove_UTF-8_CodePage_from_manifests.patch'
  '008-ki-5.99-Add-missing-header-that-broke-msvc-builds.patch'
  '009-ki-6.99-Convert-to-using-std-string-in-bitmap_info.h.patch'
  '010-ki-6.99-Add-wxString-header.patch'
#  "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git#commit=${_git_commit}"
  "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git"
)
sha256sums=('ac419b1ce1d512b2003211a936f1f2172d5d988f5db7d061169f22d20d95a5b7'
            'd8d5f4bdd0aa6d8a907710c523f6f95840636cb2ef69e5275c6ed4966f134353'
            'e03dbb58409145c8fb54991d8259f14bc0ba6b21abca24f9b914ec354c9df89c'
            '550397b8d2ba66b924933020e409e868d3e4b2352a235f329798bc631b9fd28f'
            'b32a7a0734e1d47ada234130d7b9a4ccc06f62e765bf519a4b00efa8f59c48bc'
            '833e6bdb19545c2943c3045c6854527a35ab7a5be80bdca6184c153d6b6a22ed'
            '4e9ec2f24dbe5a8020dcc56a7dac004fe0db9090eaaa985293d165e3a33d69aa'
            '00d735add20ff9de9f20eb02aeec23a0ff45a8abe39969654d4a2a8fa49c030b'
            '48c91c8e2f040ee42acd24d9f5a3cd55000165d3925f9b462ba70a26665476e5'
            '1f43decb655dc64b7bc3c7138f279db9215b0501cb3742bc2e2ce091d0696982'
            '278e20f69382c402a882d2232db5cd1bf22596615decc18b00f3894edc086ddf'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
_doc=("ca" "de" "en" "es" "fr" "id" "it" "ja" "nl" "pl" "ru" "zh")
_sub=("footprints" "symbols" "templates" "packages3D")

pkgver() {
  cd ${srcdir}/${_sourcedir}
  ( set -o pipefail
    _tag=$( head -n 200 CMakeModules/KiCadVersion.cmake | grep 'KICAD_SEMANTIC_VERSION "' | sed -e 's/KICAD_SEMANTIC_VERSION//' | sed -e 's/-unknown//' | sed -e 's/set//' | tr -d '() "' )
    _version=$( echo ${_tag} | tr -d '-' )
    printf "%s.r%s.g%s" "${_version}" $(git rev-list --count ${_tag}..HEAD) $(git rev-parse --short HEAD)
  )
}

prepare() {
  cd ${srcdir}/${_sourcedir}

  patch -Np1 -i "${srcdir}"/002-ki-6.99-cmake-fixes-for-MINGW-CLANG.patch
  patch -Np1 -i "${srcdir}"/003-ki-6.0-code-fixes-for-GNUC-CLANG.patch
  patch -Np1 -i "${srcdir}"/005-ki-6.0-rewrite-kiwin32_rc_for_clang.patch
  patch -Np1 -i "${srcdir}"/006-ki-6.0-manifest-remove-win10-11-manifest-support.patch
#  patch -Np1 -i "${srcdir}"/007-ki-6.0-Remove_UTF-8_CodePage_from_manifests.patch
#  patch -Np1 -i "${srcdir}"/008-ki-5.99-Add-missing-header-that-broke-msvc-builds.patch
  patch -Np1 -i "${srcdir}"/009-ki-6.99-Convert-to-using-std-string-in-bitmap_info.h.patch
  patch -Np1 -i "${srcdir}"/010-ki-6.99-Add-wxString-header.patch
}

build() {
  cd "${srcdir}"
  msg2 "Build kicad"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]]; then
    extra_config+=( -DOCC_INCLUDE_DIR=${MINGW_PREFIX}/include/opencascade )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export CXXFLAGS="-pipe"
    extra_config+=( -DCMAKE_CXX_FLAGS_RELEASE:STRING="-O0 -DNDEBUG" )
  fi

  extra_config+=( -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON )

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;DCMAKE_PREFIX_PATH=;DEFAULT_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DDEFAULT_INSTALL_PATH=${MINGW_PREFIX} \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DKICAD_USE_OCC="$( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] && echo "OFF" || echo "ON" )" \
    -DKICAD_SCRIPTING_WXPYTHON=OFF \
    -DKICAD_SCRIPTING=ON \
    -DKICAD_SCRIPTING_PYTHON3=ON \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
    -DPYTHON_INCLUDE_DIR=$(${MINGW_PREFIX}/bin/python.exe -c "from sysconfig import get_paths as gp; print(gp()[\"include\"])") \
    -DPYTHON_ROOT_DIR=${MINGW_PREFIX} \
    -DKICAD_BUILD_I18N=OFF \
    ../${_sourcedir}

  make VERBOSE=1 || make VERBOSE=1 --jobs=1
}

for _doclang in ${_doc[@]}; do _doc_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-doc-${_doclang}: Documentation for KiCad (mingw-w64)\""); done
for _feature in ${_sub[@]}; do _sub_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-${_feature}\""); done

eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-git() {

  msg2 \"Package kicad\"
  optdepends+=(${_doc_pkg[@]} ${_sub_pkg[@]})
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
  provides=(${MINGW_PACKAGE_PREFIX}-${_realname})

  cd \${srcdir}/build-${MSYSTEM}
  make DESTDIR=\${pkgdir} install
}"
