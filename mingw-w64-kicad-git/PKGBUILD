# Maintainer: Tim Stahlhut <stahta01@gmail.com>

_realname=kicad
 _git_commit=55679be2e319e8a315163b51038a17f407890107 # 5.99.0.r10138
#_git_commit=eac2700af6ec9a2aefcd4b4bbe5d8db35f161b42 # 5.99.0.r10361 Has GCC 12.1 silent error bug
#_git_commit=b5b76eedf767fdd6520cd9ab3bf0bd99d18b1a65 # 5.99.0.r10784 Has GCC 12.1 silent error bug
#_git_commit=769ca2d255620a101e31b953dde7340744061503 # 5.99.0.r12294 Has GCC 12.1 silent error bug
#_git_commit=222c4ce7df2433650b62a8cd7ac168b2b233f685 # 5.99.0.r12634 Has GCC 12.1 silent error bug
#_git_commit=cd8e464339bca217247e51fafc1d6aa3c79260cc # 5.99.0.r12833 fails to set default ucrt64 config
#_git_commit=e867a4fd27fba140ea90266d373ba20b4768331a # 5.99.0.r12832 works to set default ucrt64 config
_wx_basever=3.1
_sourcedir=${_realname}-git
pkgbase=mingw-w64-${_realname}-git
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=5.99.0.r10138.g55679be2e3
pkgrel=1
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.kicad.org/"
license=("GPLv3+")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
options=('!strip')
#if [[ "${MSYSTEM}" == "UCRT64" ]]; then
  #options+=('debug')
#else
  options+=('!debug')
#fi
depends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-ngspice"
  "${MINGW_PACKAGE_PREFIX}-python"
#  "${MINGW_PACKAGE_PREFIX}-wxPython"
  "${MINGW_PACKAGE_PREFIX}-wxmsw${_wx_basever}"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-freeglut"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]] && echo "${MINGW_PACKAGE_PREFIX}-opencascade")
  "${MINGW_PACKAGE_PREFIX}-cairo"
)
makedepends=(
  'git' 'make'
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-pkg-config"
  "${MINGW_PACKAGE_PREFIX}-swig"
)
source=(
  '002-cmake-fixes-for-MINGW-CLANG.patch'
  '003-code-fixes-for-GNUC-CLANG.patch'
  '005-rewrite-kiwin32_rc_for_clang.patch'
  '006-manifest-remove-win10-11-manifest-support.patch'
  '007-Remove_UTF-8_CodePage_from_manifests.patch'
  "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git#commit=${_git_commit}"
# "${_sourcedir}"::"git+https://github.com/KiCad/kicad-source-mirror.git"
)
sha256sums=('ac419b1ce1d512b2003211a936f1f2172d5d988f5db7d061169f22d20d95a5b7'
            'd8d5f4bdd0aa6d8a907710c523f6f95840636cb2ef69e5275c6ed4966f134353'
            'e03dbb58409145c8fb54991d8259f14bc0ba6b21abca24f9b914ec354c9df89c'
            '550397b8d2ba66b924933020e409e868d3e4b2352a235f329798bc631b9fd28f'
            'b32a7a0734e1d47ada234130d7b9a4ccc06f62e765bf519a4b00efa8f59c48bc'
            'SKIP')
_doc=("ca" "de" "en" "es" "fr" "id" "it" "ja" "nl" "pl" "ru" "zh")
_sub=("footprints" "symbols" "templates" "packages3D")

pkgver() {
  cd ${srcdir}/${_sourcedir}
  ( set -o pipefail
    _tag=$( head -n 200 CMakeModules/KiCadVersion.cmake | grep 'KICAD_SEMANTIC_VERSION "' | sed -e 's/KICAD_SEMANTIC_VERSION//' | sed -e 's/-unknown//' | sed -e 's/set//' | tr -d '() "' )
    _version=$( echo ${_tag} | tr -d '-' )
    printf "%s.r%s.g%s" "${_version}" $(git rev-list --count ${_tag}..HEAD) $(git rev-parse --short HEAD)
  )
}

prepare() {
  cd ${srcdir}/${_sourcedir}

#  patch -p1 -i "${srcdir}"/002-cmake-fixes-for-MINGW-CLANG.patch
#  patch -p1 -i "${srcdir}"/003-code-fixes-for-GNUC-CLANG.patch
#  patch -p1 -i "${srcdir}"/005-rewrite-kiwin32_rc_for_clang.patch
#  patch -p1 -i "${srcdir}"/006-manifest-remove-win10-11-manifest-support.patch
#  patch -p1 -i "${srcdir}"/007-Remove_UTF-8_CodePage_from_manifests.patch
}

build() {
  cd "${srcdir}"
  msg2 "Build kicad"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-aarch64* ]]; then
    extra_config+=( -DOCC_INCLUDE_DIR=${MINGW_PREFIX}/include/opencascade )
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export CXXFLAGS="-pipe"
    extra_config+=( -DCMAKE_CXX_FLAGS_RELEASE:STRING="-O0 -DNDEBUG" )
  fi

  extra_config+=( -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON )

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;DCMAKE_PREFIX_PATH=;DEFAULT_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DDEFAULT_INSTALL_PATH=${MINGW_PREFIX} \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DKICAD_USE_OCC="$( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] && echo "OFF" || echo "ON" )" \
    -DKICAD_SCRIPTING_WXPYTHON=OFF \
    -DKICAD_SCRIPTING=ON \
    -DKICAD_SCRIPTING_PYTHON3=ON \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
    -DPYTHON_INCLUDE_DIR=$(${MINGW_PREFIX}/bin/python.exe -c "from sysconfig import get_paths as gp; print(gp()[\"include\"])") \
    -DPYTHON_ROOT_DIR=${MINGW_PREFIX} \
    -DKICAD_BUILD_I18N=OFF \
    ../${_sourcedir}

  make VERBOSE=1 || make VERBOSE=1 --jobs=1
}

for _doclang in ${_doc[@]}; do _doc_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-doc-${_doclang}: Documentation for KiCad (mingw-w64)\""); done
for _feature in ${_sub[@]}; do _sub_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-${_feature}\""); done

eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-git() {

  msg2 \"Package kicad\"
  optdepends+=(${_doc_pkg[@]} ${_sub_pkg[@]})
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
  provides=(${MINGW_PACKAGE_PREFIX}-${_realname})

  cd \${srcdir}/build-${MSYSTEM}
  make DESTDIR=\${pkgdir} install
}"
