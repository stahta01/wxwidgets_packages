# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=kicad
pkgbase=mingw-w64-${_realname}-git
_sub=("symbols")
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-${_sub}-git")
pkgver=6.0.5.r13.gd05d86b8
pkgrel=1
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=("any")
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="http://www.kicad-pcb.org"
license=("GPLv3+")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "git")
source=("${_realname}-${_sub}-git"::"git+https://gitlab.com/kicad/libraries/kicad-${_sub}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}-${_sub}-git"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
#    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  for _i in ${_sub[@]}; do
    [[ -d build-${_i} ]] && rm -r build-${_i}
  done
  true
}

build_sub() {
  cd "${srcdir}"
  mkdir -p build-${1} && cd build-${1}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${1}-git
}

for _s in ${_sub[@]}; do
  eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}-git() {
    options=('!strip')
    conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}")
    build_sub ${_s}
    cd "\"\${srcdir}\"/build-${_s}"
    make DESTDIR="\"\${pkgdir}\"" install
  }"
done
